apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {

    namespace "io.github.sds100.keymapper.inputmethod.latin"
    compileSdk 34
    buildToolsVersion = '34.0.0'

    defaultConfig {
        applicationId "io.github.sds100.keymapper.inputmethod.latin"
        minSdkVersion 19
        targetSdkVersion 34
        versionCode 20
        versionName '1.4.6'
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "-debug"
        }
        ci {
            debuggable = true

            versionNameSuffix "-ci.$RUN_NUMBER"
            signingConfig signingConfigs.debug
        }
    }


    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    buildFeatures {
        aidl true
    }

    lintOptions {
        abortOnError false
    }

    ndkVersion '21.4.7075529'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    androidResources {
        noCompress 'dict'
    }
}

android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "keymapper-gui-keyboard-${variant.versionName}.apk"
    }
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    // Replaces recyclerview:1.0.0 included by above dependency
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
}

repositories {
    mavenCentral()
}
